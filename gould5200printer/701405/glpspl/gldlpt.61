;-*-MIDAS-*-
TITLE File Lister for the Gould LPT

; GLDLPT sends ASCII files to the Gould to be printed in alphanumeric mode.
; Takes JCL (only) of the form <input spec> /<flags>
; <input spec>  defaults to the current DDT print defaults.
; <flags> are optional:  /L means do page numbering, /-L turns this off;
; /! means wraparound overflow lines, /-! means truncate them;  /^ means
; print control characters as ^A for Control-A, etc., /-^ prints them
; using the hardware character font; /F means use FORTRAN printer control
; conventions.  The defaults are /L!^.
; If the program dies from an IOC error, it probably means that the Gould
; went off-line due to paper running out, an interlock error, or the
; remote PDP-11 crashed.

;; TO DO:
;; FORTRAN MODE (UGH)
;; LIST MODE (PAGE NUMBERS, ETC.)
;; TEST THE CASE OF CTL CHAR AT EOL

A=1 ? B=2 ? C=3 ? D=4 ? E=5 ? F=6 ? BC=7 ? BP=10 ? P=17
TTYO=13 ? DSKI=14 ? CHAOSR=15 ? CHAOST=16
PKTPTR==D
PKTCNT==C
HPOS==PKTCNT
VPOS==F

TOPMAR=5 ? BOTMAR=3 ? LINEL=132. ? PAGEL=62.+TOPMAR ? RTMAR==8
TAB1==10. ? TAB2==20.

CHAWIN==3			;RECEIVE WINDOW SIZE (NOT REALLY NEEDED)
CUTBIT==1_10.			;CUT COMMAND BIT
R11HN==100			;REMOTE 11 HOST NUMBER AND
R11SN==1			;REMOTE 11 SUBNET NUMBER

$$RFN==1			;enable file name parser
$$SWITCH==1			;enable flag reading
$$PFN==1			;enable file name printer
$$MNAME==1			;enable machine name output

.INSRT SYSENG;RFN >

$$OUT==1			;output date and time

.INSRT SYSENG;DATIME >

DEFINE SYSCAL NAME,ARGS
	.CALL [SETZ ? SIXBIT/NAME/ ? ARGS ((SETZ))]
	 .LOSE 1000
TERMIN

DEFINE	PKTPRT ARG
 IRPC X,,[ARG]
     MOVEI A,"X
     PUSHJ P,PUTCHR
 TERMIN
TERMIN

DEFINE  SPACES N
	MOVEI A,40
	MOVEI B,N
	PUSHJ P,PUTCHR
	SOJG B,.-1
TERMIN

; INCLUDE CHAOS NET DEFINITIONS
.INSRT SYSTEM;CHSDEF >

%GOALP==2

;;; Beginning of the main program

GO:	MOVEI P,PDL
	PUSHJ P,SETDFL		;SET DEFAULTS FROM DDT, ETC.
	PUSHJ P,GETFN		;GET JCL AND FILE NAMES
	PUSHJ P,CHAOPN		;OPEN THE CHAOS NET CONNECTION
	PUSHJ P,MAPIN		;MAP IN THE INPUT FILE
	.VALUE [ASCIZ/:PROCED /]	;PROCEED WHILE RUNNING
	PUSHJ P,ZAPOUT		;SEND OUT THE DATA
	PUSHJ P,CHACLS		;CLOSE THE CHAOS NET CONNECTION
	.BREAK 16,124000	;BYE

;;; GET FILE DEFAULTS FROM DDT
SETDFL:	SETZM TRCFLG		;TRUNCATION OFF
	SETOM LSTFLG		;LISTING ON
	SETOM CTLFLG		;CONTROL CHAR HACKS ON
	SETZM FTNFLG		;FORTRAN FLAG OFF
	.BREAK	12,[6,,PRNTFN]	; READ DDTS PRINT DEFAULTS (WIERD ORDER)
	MOVE A,FN1		;SWAP SNAME TO FN2, FN2 TO FN1, FN1 TO SNAME
	EXCH A,SNAME
	EXCH A,FN2
	MOVEM A,FN1
	PUSHJ P,RFN"RMNAME	;READ MACHINE NAME
	PUSHJ P,DATIME"TIMGET	;GET THE TIME
	MOVEM A,TIME
	POPJ P,

;;; OPEN A CONNECTION TO THE GOULD (IF POSSIBLE)

CHAOPN:	SYSCAL CHAOSO,[	1000,,CHAOSR ? 1000,,CHAOST ? 1000,,CHAWIN]
	SYSCAL PKTIOT,[	1000,,CHAOST ? 1000,,RFCPKT]	;SEND RFC
	MOVEI A,3*30.		;3 SECOND TIMEOUT
	SYSCAL NETBLK,[	1000,,CHAOSR ? 1000,,%CSRFS ? A ? 2000,,B ? 
			2000,,C]
	CAIN B,%CSOPN
	 POPJ P,
	JUMPN C,CLSMSG		;IF WE DIDN'T TIME OUT, GO PRINT THE CLOSE MSG
	.VALUE [ASCIZ /:
Attempt to connect to the Gould printer has timed-out.
Either the remote PDP-11 or the Chaos net is down.

:Killî/]			;SIMPLE TIMEOUT
	JRST CHAOPN		;TRY AGAIN IF HE ALT-P'S THE JOB

CLSMSG:	CAIE B,%CSCLS		;ARE WE IN A CLOSE STATE? IF NOT, BARF.
	 .VALUE [ASCIZ /:Chaos net software bug.  Please send :BUG GLDLPT.KILL /]
	SYSCAL PKTIOT,[1000,,CHAOSR ? 1000,,DATPKT]	;GET THE CLOSE PACKET
	SYSCAL OPEN,[1000,,TTYO ? 5000,,.UAO ? ['TTY,,]]	;OPEN THE TTY
	MOVEI A,CLSLEN
	LDB B,[$CPKNB DATPKT]	;BYTE POINTER TO NUMBER OF BYTES
	SYSCAL SIOT,[1000,,TTYO ? [440700,,CLSMES] ? A]
	SYSCAL SIOT,[1000,,TTYO ? [441000,,DATPKT+%CPKDT] ? B]
	MOVEI A,15		;SEND OUT A FINAL CR
	SYSCAL IOT,[1000,,TTYO ? A]
	.BREAK 16,124000

CLSMES:	ASCII/Request for connection refused because:î/
CLSLEN==<.-CLSMES>*5		;LENGTH OF THE MESSAGE IN CHARACTERS

;;; CLOSE THE GOULD CONNECTION

CHACLS:	SYSCAL FINISH,[1000,,CHAOST]	;FORCE OUT ALL PACKETS
	SYSCAL PKTIOT,[1000,,CHAOST ? 1000,,CLSPKT]	;TELL THE OTHER END
	SYSCAL CLOSE,[1000,,CHAOST]	;CLOSE THE CHANNELS
	SYSCAL CLOSE,[1000,,CHAOSR]
	POPJ P,

; Put a disk file into this job's page map
	
MAPIN:	MOVEI F,PRNTFN
	SYSCAL OPEN,[ 1000,,DSKI ? 5000,,4 ? (F) ? 1(F) ? 2(F) ? 3(F)]
	SYSCAL FILLEN,[1000,,DSKI ? 2000,,A ]
	MOVEI BC,(A)
	IMULI BC,5		;MAKE INTO A CHAR COUNT
	ADDI A,1777
	LSH A,-10.		; NUMBER OF PAGES
	HRLOI A,-1(A)		; MAKE AN AOBJN POINTER -<A>,,2 (HAKMEM)
	EQVI A,BUFFER/2000
	SYSCAL CORBLK,[	1000,,<%CBNDR+%CBCPY+%CBNDW>	; REQUEST READ ACCESS 
			[-1] ? A ? 1000,,DSKI]	; FOR HIGH THRU PUTT
	SYSCAL CLOSE,[ 1000,,DSKI]
	POPJ P,

;;; PROCESS AND SEND OUT THE FILE

ZAPOUT:	MOVEI VPOS,PAGEL+BOTMAR
	MOVE BP,[440700,,BUFFER]	;BYTE POINTER
	MOVE PKTPTR,[441000,,%CPKDT+DATPKT]
	SETZ HPOS,
	SETZM PAGCNT
	PUSHJ P,FF
	SKIPE FTNFLG
	 PUSHJ P,GETCHR		;GOBBLE FIRST CHAR IN FTN MODE
ZLOOP:	CAIL HPOS,LINEL		;CHECK HPOS FOR WRAPAROUND
	 PUSHJ P,WRAP
	CAIL VPOS,PAGEL		;PAGE OVERFLOW
	 PUSHJ P,FF
	PUSHJ P,GETCHR
	CAIN A,15		;CR?
	 JRST NEWLIN		;YES, DO A NEW LINE
	CAIN A,12		;LF?
	 JRST ENDTST		;YES, IGNORE IT COMPLETELY
	CAIN A,14		;FF?
	 JRST FF1
	CAIN A,33		;ALTMODE?
	 MOVEI A,"$		;TURN IT INTO DOLLAR SIGN
	CAIN A,11		;TAB?
	 JRST TAB
	CAIN A,177		;RUBOUT?
	 PUSHJ P,GETCHR		;QUOTES THE NEXT CHARACTER
	CAIGE A,40		;GREATER THAN 40?
	 SKIPN CTLFLG		;SPECIAL HANDLING OF CONTROLS?
	  CAIA			;NO SPECIAL HANDLING
	   PUSHJ P,CTLQOT
	PUSHJ P,PUTCHR

ENDTST:	JUMPGE BC,ZLOOP		;CONTINUE LOOPING IF SOME BYTES LEFT
	PUSHJ P,FF		;FEED THE LAST PAGE
	POPJ P,			;DONE

NEWLIN:	PUSH P,BP
	ILDB B,BP		;LOAD THE NEXT CHARACTER
	POP P,BP
	CAIN B,12		;LF?
	 PUSHJ P,GETCHR		;YES, GOBBLE IT
	PUSHJ P,FINLIN		;FINISH THE LINE
	JRST ENDTST		;AND CONTINUE

FF1:	PUSHJ P,FF
	JRST ENDTST

TAB:	PUSHJ P,SPCOUT		;JUST OUTPUT SPACES UNTIL HPOS IS A FACTOR OF 8
	TRNE HPOS,7		;DOESN'T CHECK FOR EOL (SHOULD IT?)
	 JRST TAB
	JRST ENDTST

GETCHR:	ILDB A,BP		;PICK UP A CHARACTER
	SOSGE BC		;DECREMENT THE COUNT OF INPUT CHARS
	 MOVEI A,15		;RETURN A CR AS THE LAST CHAR
	POPJ P,

PUTCHR:	IDPB A,PKTPTR		;DEPOSIT THE CHAR IN THE PACKET
	AOS HPOS		;STEP THE HPOS (AND CHAR COUNT)
	POPJ P,

;;; CHECK FOR QUOTING CONTROL CHARS
CTLQOT:	CAILE HPOS,LINEL-2	;CHECK END OF LINE CONDITION
	 JRST [	MOVEI B,.+1
		PUSH P,B	;PUSH THE CONTINUATION ADDRESS
		SKIPN TRCFLG	;TRUNCATE
		 JRST FINLIN	;NO, JUST WRAP AND CONTINUE
		POP P,B		;THROW AWAY PUSH .+1
		MOVEI B,ENDTST
		MOVEM B,(P)	;SMASH THE RETURN
		JRST WRAP1]	;SKIP THE REST OF THE LINE
	PUSH P,A		;SAVE A
	MOVEI A,"^		;OUTPUT A CARET PREFIX
	PUSHJ P,PUTCHR
	POP P,A
	TRO A,100		;TURN INTO AN ALPHA CHAR
	POPJ P,

;;; LINE OVERFLOW (WRAPAROUND)
WRAP:	SKIPN TRCFLG		;TRUNCATING?
	 JRST FINLIN		;NO, JUST BREAK THE LINE
WRAP1:	PUSHJ P,GETCHR
	CAIE A,15		;LOOK FOR CR
	 JRST WRAP1
;	JRST FINLIN		;JCALL FINLIN

;;; NEW LINE (SENDS OUT THE CURRENT LINE)

FINLIN:	SKIPE FTNFLG		;FORTRAN MODE?
	 JRST FTNLIN		;YES, GO CHECK FOR PRINTER CONTROLS
FINLI1:	SKIPN PKTCNT		;EMPTY PACKET?
	 PUSHJ P,SPCOUT		;YES, PUT AT LEAST ONE SPACE IN
	TRNE PKTCNT,1		;ODD NUMBER OF BYTES?
	 PUSHJ P,SPCOUT		;YES, PAD OUT WITH A SPACE
	DPB PKTCNT,[$CPKNB DATPKT]	;DEPOSIT THE BYTE COUNT
	MOVE B,DATPKT		;GET THE FIRST WORD
	SKIPN VPOS		;ARE WE ON THE FIRST LINE?
	 TLO B,CUTBIT		;YES, SET THE CUT BIT IN THE OPCODE
	MOVEM B,DATPKT
	SYSCAL PKTIOT,[1000,,CHAOST ? 1000,,DATPKT]
	TLZ B,CUTBIT		;CLEAR THE CUT BIT
	MOVEM B,DATPKT		;RESTORE
	MOVE PKTPTR,[441000,,%CPKDT+DATPKT]	;RESET
	SETZ HPOS,		;ZERO THE HPOS AND PACKET COUNT
	AOS VPOS		;STEP THE LINE COUNTER
	POPJ P,

;;; CHECK FOR FORTRAN PRINTER CONTROLS IN COLUMN 1
FTNLIN:	PUSHJ P,FINLI1		;FINISH THE CURRENT LINE FIRST
	PUSHJ P,GETCHR		;GET THE FIRST CHAR OF THE LINE
	CAIN A,"1		;IS IT A 1 (MEANS DO FF)
	 JRST FF		;YES, BEGIN A NEW PAGE
	CAIN A,"0		;IS IT A 0 (MEANS SKIP A LINE)
	 JRST FINLI1		;YES, PUT OUT A BLANK LINE
	POPJ P,			;OTHERWISE, IGNORE IT

SPCOUT:	PUSH P,A		;PUT OUT A SPACE, SAVING A
	MOVEI A,40
	PUSHJ P,PUTCHR
	POP P,A
	POPJ P,

;;; FORM FEED OPERATION
FF:	CAIGE VPOS,PAGEL+BOTMAR	;ARE WE AT THE BOTTOM OF THE PAGE YET?
	 JRST [	PUSHJ P,FINLI1	;PUT OUT A BLANK LINE
		JRST .-1 ]	;KEEP PUTTING OUT BLANK LINES
	SETZ VPOS,		;ZERO THE VERTICAL POS
	CAIGE VPOS,TOPMAR
	 JRST [	PUSHJ P,FINLI1	;PUT OUT A BLANK LINE
		JRST .-1 ]	;KEEP PUTTING OUT BLANK LINES
	AOS PAGCNT		;INCREMENT THE PAGE COUNT
	SKIPN LSTFLG		;IF LIST MODE, PUT OUT A PAGE NUMBER
	 POPJ P,		;OTHERWISE, JUST RETURN

;;; PUT OUT PAGE HEADING
;;; NOTE, D=PKTPTR IS SET UP FOR THE LIB ROUTINES

	PUSH P,PKTPTR		;CLEAR OUT THE PACKET BUFFER
	SPACES LINEL
	POP P,PKTPTR		;AND BACK TO THE BEGINNING
	MOVE A,TIME		;PICK UP THE TIME
	PUSHJ P,DATIME"TWDASC	;PRINT THE DATE AND TIME
	SPACES TAB1		;OUTPUT TAB1 SPACES
	PKTPRT FILE: 
	MOVEI B,PRNTFN		;POINTER TO THE FILE NAME BLOCK
	PUSHJ P,RFN"PFNMCH	;PRINT THE FILE NAME
	SPACES TAB2
	PKTPRT PAGE 
	MOVE A,PAGCNT		;NOW OUTPUT THE PAGE NUMBER (FROM DEC-10 MANUAL)
	MOVEI B,FFFIN
	PUSH P,B		;FINISH UP HERE WHEN DONE BELOW
PAGNLP:	IDIVI A,10.		;DIVIDE BY TEN
	HRLM B,(P)		;REMAINDER IN B IN LH (RETURN PC IN RH)
	SKIPE A			;DONE WHEN NOTHING LEFT
	 PUSHJ P,PAGNLP
	MOVEI B,(A)		;SAVE A IN B
	HLRZ A,(P)		;GET THE HIGHEST DIGIT FIRST
	ADDI A,"0		;TURN IT INTO A DIGIT
	CAIGE B,"0		;WAS LAST DIGIT FOR REAL?
	 JRST [ CAIG A,"0	;NO, SUPPRESS LEADING ZEROS
		 MOVEI A,40	;BY TURNING THEM INTO SPACES
		JRST .+1]
	JRST PUTCHR		;JCALL - STUFF IT IN THE BUFFER

FFFIN:	MOVEI HPOS,LINEL	;OUTPUT THE FULL LINE
	PUSHJ P,FINLI1		;PUT OUT THE HEADING LINE
	JRST FINLI1		;AND A BLANK ON

; FILE NAME PARSING USING THE STANDARD LIBRARY ROUTINE

GETFN:	.BREAK 12,[..RJCL,,JCLBUF]	; READ JCL
	SETZM MINUSF			;CLEAR THE SIGN FLAG
	SKIPN JCLBUF			; SKIP IF WE READ SOMETHING ANYTHING
	 JRST HELP			; IF NOT, THE LOSER NEEDS HELP
	MOVEI B,PRNTFN	                ; RESULTING FN'S GO HERE
	MOVE D,[440700,,JCLBUF]	        ; TEXT OF FN PROVIDED BY USER
	PUSHJ P,RFN"RFN		        ; PARSE FILE SPEC
	POPJ P,

PSIXTP:
RSIXTP:	CAIN A,"/       ; SKIP RETURN IF / (FOR FLAG READING)
	 AOS (P)
	POPJ P,

SWITCH:	CAIN A,"-
	 JRST [	SETOM MINUSF
		POPJ P,]
	CAIN A,"?		; PROCESS SWITCHES
	 JRST HELP
	CAIN A,"^		;SPECIAL HANDLING OF CONTROL CHARS?
	 JRST [	SETOM CTLFLG	;YES
		SKIPE MINUSF	;CHECK THE MINUS FLAG
		 SETZM CTLFLG	;NO
		JRST SWDONE]
	CAIN A,"!		;TRUNCATE LINES?
	 JRST [	SETZM TRCFLG	;CLEAR TRUNCATION FLAG
		SKIPE MINUSF	;CHECK THE MINUS FLAG
		 SETOM TRCFLG	;SET IT IF THE MINUS FLAG WAS SET
		JRST SWDONE]
	CAIN A,"L		;LISTING FLAG
	 JRST [	SETOM LSTFLG	;NORMALLY ON
		SKIPE MINUSF
		 SETZM LSTFLG
		JRST SWDONE]
	CAIN A,"F
	 JRST [	SETOM FTNFLG	;TURN ON FORTRAN FLAG
		SETZM LSTFLG	;TURN OFF LIST FLAG
		SETOM TRCFLG	;AND TRUNCATE
		JRST SWDONE]

SWDONE:	SETZM MINUSF		;RESET
	POPJ P,

HELP:	SYSCAL OPEN,[1000,,TTYO ? 5000,,.UAO ? ['TTY,,]]
	MOVE A,[440700,,HLPMSG]
	MOVEI B,HLPLEN
	SYSCAL SIOT,[1000,,TTYO ? A ? B]
	.BREAK 16,124000

DEFINE HELPMSG MSG
       HLPLEN==.LENGTH |MSG|
       ASCII |MSG|
TERMIN

HLPMSG:
HELPMSG [File Printer for the Gould LPT:
Sends ASCII files to the Gould to be printed in alphanumeric mode.
Takes JCL (only) of the form <input file spec> /<flags>
<input file spec>  defaults to the current DDT print file defaults.
<flags> are optional:  /L means do page numbering, /-L turns this off;
/! means wraparound overflow lines, /-! means truncate them;  /^ means
print control characters as ^A for Control-A, etc., /-^ prints them
using the hardware character font; /F means use FORTRAN printer control
conventions.  The defaults are /L!^.
If the program dies from an IOC error, it probably means that the Gould
went off-line due to paper running out, an interlock error, or the
remote PDP-11 crashed.
]

; DATA STRUCTURES

VARIABLES
CONSTANTS

;;; VARIABLES
CTLFLG:	0			;CONTROLS PRINTING OF CONTROL CHARS
FTNFLG:	0			;CONTROLS THE USE OF FORTRAN PRINTER CONTROLS
LSTFLG:	0			;LISTING FLAG (PAGE NUMBERING, ETC)
TRCFLG:	0			;TRUNCATION FLAG
MINUSF:	0			;MINUS SWITCH FLAG
PAGCNT:	0

;;; REQUEST FOR CONNECTION PACKET
RFCPKT:
.BYTE 16.
	%CORFC_8		;OP CODE
	6			;BYTE COUNT (3 WORDS)
	R11SN_8+R11HN		;DESTINATION HOST: SUBNET AND HOST NUM OF R11
	0			;DESTINATION INDEX: 0 (UNKNOWN AT THIS POINT)
	0			;SOURCE HOST (THE NCP SETS THIS)
	0			;SOURCE INDEX (NCP SETS)
	0			;PACKET NUMBER (NCP SETS)
	CHAWIN			;ACK PACKET NUMBER (REALLY THE WINDOW SIZE)
.BYTE 8
	"G ? "O ? "U ? "L ? "D ? 40	;CONTACT NAME "GOULD "
.BYTE

;;; CLOSE PACKET
CLSPKT:
.BYTE 16.
	%COCLS_8		;OP CODE
	0
	BLOCK 7			;THESE WORDS SET BY THE NCP
				;NO DATA AREA
.BYTE

;;; DATA PACKET
DATPKT:
.BYTE 16.
	<%CODAT+100+%GOALP>_8	;OP CODE IS DATA AND ALPHA NUMERIC (100 is for 16 bits)
	0			;BYTE COUNT, SET IN THE PROGRAM ABOVE
	BLOCK 6			;REMAINING SLOTS ARE SET BY THE NCP
	BLOCK %CPMXC/2		;DATA AREA
.BYTE

DATDAT=DATPKT+%CPKDT		;POINTER TO DATA AREA OF DATA PACKET


;;; DATA BLOCKS

PRNTFN:				;PRINT FILE NAME BLOCK
DEV:	0
FN1:	0
FN2:	0
SNAME:	0

MNAME:	0			;MACHINE NAME (USE BY THE FN PRINTER)
TIME:	0			;TIME

JCLBUF:	BLOCK 20.

PDL:	BLOCK 100

BUFFER=4000

	END GO
