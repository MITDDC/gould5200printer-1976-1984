	TITLE SPOOLER LAUNCHER
; SPOOLER LAUNCHER TO BE CALLED BY THE MAILER

; AC'S
A=1 ? B=2

; I/O CHANNELS
DSKI=1 ? USRI=2 

DEFINE SYSCAL NAME,ARGS
	.CALL [SETZ ? SIXBIT/NAME/ ? ARGS ((SETZ))]
TERMIN

JOBUNM:	SIXBIT /GLPTR/
JOBSNM:	SIXBIT /GLPT/
TS:	SIXBIT /TS/
SPOOLR:	'SPOOLR

GO:	SYSCAL OPEN,[1000,,USRI ? 5000,,14 ? ['USR,,0] ? JOBUNM ? SPOOLR]
	 JRST LAUNCH			; NO SUCH JOB, OK GO LAUNCH SPOOLER
	.USET USRI,[.RUSTP,,A]		; SEE IF HE IS STOPPED.
	SKIPN A				; IF WORD IS ZERO, JOB IS RUNNING
	 JRST BYE			; OK, SO JUST RETURN
	.USET USRI,[.RUIND,,A]		; KILL THE DEAD JOB
	.GUN A,
LAUNCH:	SYSCAL OPEN,[1000,,USRI ? 5000,,4 ? ['USR,,0]? 1000,,0 ? SPOOLR]
	 JRST BYE
	SYSCAL OPEN,[1000,,DSKI ? 5000,,6 ? ['DSK,,0] ? TS ? SPOOLR ? JOBSNM]
	 JRST BYE	; SHOULDN'T LOSE IF FILES ARE WINNING
	SYSCAL LOAD,[1000,,USRI ? 1000,,DSKI]	; ONLY LOSES IF JOB IS FOREIGN
	 JRST BYE
	MOVE A,[-1,,B]
	.IOT DSKI,A	; GET START-ADDRESS
	HRRZS B
	.USET USRI,[.SUPC,,B]	; SET START-ADDRESS
	.USET USRI,[.STTY,,[%TBNVR,,]]	; TELL IT NOT TO HANG WAITING FOR THE TTY
	.USET USRI,[.SSNAM,,JOBSNM]
	SYSCAL DISOWN,[1000,,USRI ? 5000,,17]	; STARTS AFTER DISOWNING
	 JFCL

BYE:	SYSCAL LOGOUT,[1000,,0]
BYE1:	.BREAK 16,40000


	END GO
