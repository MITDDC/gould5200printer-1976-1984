	TITLE UNSPOOLER FOR A REMOTE GRAPHICS LINE PRINTER

;   THE PURPOSE OF THIS PROGRAM IS TO UNSPOOL PREPROCESSED FILES TO
; A REMOTE GRAPHICS PRINTER (CURRENTLY THE BDLG. 38 GOULD 5200).
; IT LOOKS FOR FILES DSK:GLPT;[GLPT] >, AND, IF FOUND, PUTS THEM
; OUT TO A REMOTE DEVICE BY INSERTING THE FILE INTO ITS PAGE MAP
; AND DOING AN 8-BIT SIOT TO THE DEVICE (CURRENTLY TTY T16, EVENTUALLY
; TO A DESTINATION ON THE CAIOS NET).  UPON COMPLETION, THE FILE IS
; DELETED.  THE FIRST WORD OF THE FILE IS A HEADER WORD CONTAINING
; INFORMATION SUCH AS THE TYPE OF FILE (BIT IMAGE RUN-LENGTH ENCODED,
; OR ASCII FOR THE HARDWARE CHARACTER GENERATION MODE OF THE REMOTE
; PRINTER), AND A COUNT OF BYTES IN THE FILE. IF THERE ARE NO FILES,
; THE PROGRAM SLEEPS FOR A WHILE.
;   ANOTHER TASK OF THIS PROGRAM IS TO START UP A LISP PROGRAM TO 
; PROCESS FILES INTO THE 8-BIT FORMAT.  IF THERE IS MAIL FOR THE
; SPOOLER (GLPT;GLMAIL >) AND THE JOB GLPTR SPOOLR DOES NOT EXIST,
; IT IS STARTED BY CREATING AN INFERIOR JOB, AND DISOWNING IT.
; THE GLPT JOB IS A DUMPED NEWIO LISP WHICH READS ITS MAIL AND
; PROCESSES FILES ACCORDING TO INSTRUCTIONS GIVEN IN THE MAIL.
; IF IT HAS NOTHING TO DO FOR A WHILE, GLPT WILL KILL ITSELF.
;   THIS PROGRAM SHOULD NORMALLY BE ONLY ONE BLOCK LONG, FLUSHING
; ITS MAPPED IN CORE PAGES WHEN THEY ARE NO LONGER IN USE.

;   IMPROVEMENTS NEEDED FOR CAIOS NET OPERATION:  ESTABLISH CONNECTION
; (CONTACT) TO THE REMOTE PDP-11 PRINTER/PLOTTER DRIVER SOFTWARE,
; HANDLE PRINTER NOT READY, ETC.  CLI TO PERSONS FOR HELP IF PRINTER
; NOT READY.

; OTHER IDEAS:  WHEN CHECKING FOR EXISTENCE OF SPOOLR, SEE IF IT IS RUNNING.
;  IF NOT, GUN, THEN RELOAD.  ALSO ACK. HACKERY CAN BE FLUSHED WHEN MULTIPLEXING
;  IS IMPLEMENTED SINCE I.T.P. WILL HANDLE THAT.


SUBTTL SYMBOLS AND MACROS

VERSIO==.FNAM2
; AC'S
A=1 ? B=2 ? C=3 ? D=4 ? E=5 ? F=6 ? P=17

; I/O CHANNELS
DSKI=1 ? LPTIN=2 ? LPTOUT=3 ? USRI=4 ? USRO=5


DEFINE INSIRP A,B
IRPS XXX,,[B]
     A,XXX
TERMIN TERMIN

DEFINE SYSCAL NAME,ARGS
	.CALL [SETZ ? SIXBIT/NAME/ ? ARGS ((SETZ))]
TERMIN

MAPPAG=2	; MAP IN PAGES STARTING AT PAGE 2
BUFORG=MAPPAG*2000 ; ORIGIN OF MAPPED IN PAGES
SLPTIM=3.	; SLEEP TIME IN SECONDS
CONSLP=3.	; SLEEP FOR TEN SECONDS WHILE TRYING TO CONNECT
ACKCHT=301	; ACKNOWLEDGEMENT CODE TO SEE IF THE PDP-11 IS THERE
ACKCHR=1	; RETURN ACK CHAR
%CBLOK=2000	; SHOULDN'T NEED THIS IN THE NEXT I.T.S.
BUSRC=100000	; USER STOP BIT IN .USTP
MAXSLP=200	; MAX NUMBER OF SLEEP PERIODS

SUBTTL DEVICE AND FILE CONSTANTS
	LOC 100

FDEV:	'DSK,,0
FN1:	SIXBIT /[GLPT]/
FN2:	SIXBIT /</
FSNAME:	SIXBIT/GLPT/

LPTDEV:	'T16,,
DSK:	'DSK,,0
USR:	'USR,,0

GSPFIL:	SIXBIT /[GLPT]/
GTFILE:	SIXBIT />/
LTFILE:	SIXBIT /</

JOBFN1:	SIXBIT /TS/
JOBFN2:	SIXBIT /SPOOLR/
JOBSNM:	SIXBIT /GLPT/
JOBJNM:	'SPOOLR

GUNAME:	SIXBIT /GLPTR/
GSNAME:	SIXBIT/GLPT/
GJNAME:	'UNSPLR

MAILFN:	'GLMAIL
QFILE:	SIXBIT /[QUEUE/

SLPCNT:	-MAXSLP		; COUNT OF SLEEP PERIODS

SUBTTL MAIN FILE PROCESSING LOOP

GO:	SYSCAL OPEN,[1000,,USRI ? 5000,,14 ? USR ? GUNAME ? GJNAME]
	 JRST LAUNCH			; NO SUCH JOB, OK LAUNCH UNSPOOLER
	.SUSET [.ROPTION,,B]		; GET OPTION BITS
	.USET USRI,[.RUSTP,,A]		; SEE IF HE IS STOPPED.
	TLNE A,BUSRC			; IF BIT IS ZERO, JOB IS RUNNING
	 JRST DEAD
	TLNN B,OPTDDT
	 JRST BYE1
	.VALUE [ASCIZ /:UNSPOOLER ALREADY LAUNCHED AND RUNNING.î:KILLî/]
DEAD:	.USET USRI,[.RUIND,,A]		; KILL THE DEAD JOB
	.GUN A,
	SYSCAL CLOSE,[1000,,USRI]
	 JFCL
LAUNCH:	TLNE B,OPTDDT
	 JRST INFLOD
	.VALUE [ASCIZ /17/]		; DISOWN MYSELF
INIT:	MOVE P,[-PDLEND,,PDL-1]		; INITIALIZE THE PDL
	.SUSET [.SUNAM,,GUNAME]		; CHANGE MY UNAME TO GLPTR
	.SUSET [.SJNAM,,GJNAME]		; MAKE ME LOOK LIKE AN UNSPOOLER
	.SUSET [.SSNAM,,GSNAME]		; FOR THE BENEFIT OF PEEK

; MAIN LOOP FOR MAPPING, OUTPUTING, AND DELETING FILES
	SETZM CONFLG'
WORKLP:

CONTIN:	SYSCAL OPEN,[1000,,DSKI ? 5000,,4 ? DSK ? GSPFIL ? LTFILE ? GSNAME]
	 JRST NOWORK	; NO WORK TO DO, GO SEE ABOUT OTHER TASKS OR SLEEP
			; WE ASSUME THE ONLY POSSIBLE ERROR IS FILE NOT FOUND
	SKIPN CONFLG
	 PUSHJ P,CONECT		; CONNECT TO TTY LINE OR CAIOS DESTINATION
	MOVNI A,MAXSLP
	MOVEM A,SLPCNT
	SYSCAL RFNAME,[ [-1] ? 1000,,DSKI ? 2000,,FDEV ? 2000,,FN1 ? 2000,,FN2 ? 2000,,FSNAME]
	 JFCL		; SO WHAT IF IT DOESN'T WIN?
	SYSCAL FILLEN,[1000,,DSKI ? 2000,,A]	; GET NUMBER OF WORDS IN THE FILE
	 JFCL
	JUMPE A,SPDONE				; IGNORE NULL FILES
	MOVE B,A				; SAVE IN B FOR SIOT
	ADDI A,1777
	LSH A,-10.				; NUMBER OF PAGES
	CAILE A,376
	 MOVEI A,376
	MOVNS A		; COMPUTE AOBJN POINTER OF -<#PAGES>,,MAPPAG
	HRLS A
	HRRI A,MAPPAG
	MOVE C,A	; SAVE AOBJN PTR FOR DELETING PAGES LATER
GETCOR:	SYSCAL CORBLK,[	1000,,<%CBNDR+%CBLOK>	; REQUEST READ ACCESS AND PAGE LOCK
			[-1] ? A ? 1000,,DSKI]	; FOR HIGH THRU PUTT
	 JRST NOCORE
	LSH B,2		; WORD COUNT * 4
	MOVE D,[441000,,BUFORG]
	SYSCAL SIOT,[1000,,LPTOUT ? D ? B]
	 JFCL
	SYSCAL CORBLK,[1000,,0 ? [-1] ? C ]	; DELETE PAGES
	 JFCL
SPDONE:	SYSCAL DELETE,[FDEV ? FN1 ? FN2 ? FSNAME] ; DELETE THE FILE
	 JFCL	; JUST IGNORE IT IF WE CAN'T DELETE IT
	SYSCAL CLOSE,[1000,,DSKI]	; FILE ACTUALLY GOES AWAY NOW
	 JFCL	; NEVER FAILS
	JRST CONTIN

NOWORK:	PUSHJ P,JOBCHK	; GO CHECK ON STATUS OF GLPTR JOB
	PUSHJ P,SLEEPR
	JRST WORKLP

SUBTTL ESTABLISH CONNECTION TO REMOTE LPT DEVICE

; OPEN A CHANNEL TO THE PDP-11 AND VERIFY THAT IT IS RUNNING.  IF NOT
; SLEEP A WHILE AND TRY AGAIN.  IN THE CAIOS IMPLEMENTATION, SEE IF
; A CONNECTION EXISTS.  IF NOT, CONTACT THE PRINTER DRIVER IN THE PDP-11.

CONECT:	SYSCAL OPEN,[1000,,LPTOUT ? 5000,,45 ? LPTDEV] ; UNIT SUPERIMAGE OUTPUT
	 JRST LPTNA	; GLPT NOT AVAILABLE EVIDENTLY
	MOVEI A,33
	.IOT LPTOUT,A
	MOVEI A,14
	.IOT LPTOUT,A
	SYSCAL OPEN,[1000,,LPTIN ? 5000,,7004 ? LPTDEV]	; ACKNOWLEDGEMENT CHANNEL
			; IMAGE UNIT INPUT, DON'T WAIT IF NOTHING THERE
	 JRST LPTNA	; IF OPEN FOR OUTPUT, WHY NOT INPUT?
ACK11:	MOVEI A,%TDQOT	; SEND OUT A REQUEST FOR ACKNOWLEDGEMENT
	.IOT LPTOUT,A
	MOVEI A,ACKCHT
	.IOT LPTOUT,A
	.IOT LPTIN,A	; GET A RESPONSE
	CAIN A,ACKCHR
	 JRST CONEXT	; ALL CONNECTED TO THE PDP-11
	MOVEI C,CONSLP*30.
	.SLEEP C,
	JRST ACK11

CONEXT:	SYSCAL CLOSE,[1000,,LPTIN]
	JFCL
	SETOM CONFLG
	POPJ P,

;DISCON:	SYSCAL IOT,[1000,,LPTOUT ? 206]
;	SYSCAL CLOSE,[1000,,LPTOUT]
;	 JFCL
;	POPJ P,
	
SUBTTL HANDLER OF VARIOUS ANOMALOUS CONDITIONS

LPTNA:	PUSHJ P,SLEEPR
	JRST CONECT

NOCORE:	PUSHJ P,SLEEPR	; TRY LATER
	JRST GETCOR

SLEEPR:	SYSCAL OPEN,[1000,,USRI ? 5000,,14 ? USR ? GUNAME ? JOBJNM]
	 JRST INCSLP
	.USET USRI,[.RUSTP,,A]		; SEE IF HE IS STOPPED.
	TLNE A,BUSRC			; IF BIT IS ZERO, JOB IS RUNNING
	 JRST INCSLP
	SYSCAL CLOSE,[1000,,USRI]
	 JFCL
	JRST SLEEP
INCSLP:	AOSL SLPCNT			; COUNT ONLY INC WHEN SPOOLER DEAD
	 JRST BYE
SLEEP:	MOVEI C,SLPTIM*30.
	.SLEEP C,
	POPJ P,
BYE:	SYSCAL LOGOUT,[1000,,0]
BYE1:	.BREAK 16,40000

SUBTTL GLPTR CREATOR

; CHECK ON GLPTR JOB, START IT UP IF NECESSARY

JOBCHK:	SYSCAL OPEN,[1000,,DSKI ? 5000,,.UII ? DSK ? MAILFN ? GTFILE ? GSNAME]
	 JRST PENDG			; NO MAIL, BUT CHECK PENDING QUEUE
JOBRT:	SYSCAL CLOSE,[1000,,DSKI]
	 JFCL
	SYSCAL OPEN,[1000,,USRI ? 5000,,14 ? USR ? GUNAME ? JOBJNM]
	 JRST STGLPT
	.USET USRI,[.RUSTP,,A]		; SEE IF HE IS STOPPED.
	TLNN A,BUSRC			; IF BIT IS ZERO, JOB IS RUNNING
	 JRST JOBOK
	.USET USRI,[.RUIND,,A]		; A DEAD JOB, SO GUN IT.
	.GUN A,
	SYSCAL CLOSE,[1000,,USRI]
	 JFCL
	JRST STGLPT

JOBOK:	SYSCAL CLOSE,[1000,,USRI]	; SPOOLER IS UP AND RUNNING.
	 JFCL
	POPJ P,

PENDG:	SYSCAL OPEN,[1000,,DSKI ? 5000,,.UII ? DSK ? QFILE ? SIXBIT /FILE/ ? GSNAME]
	 POPJ P,			; NO PENDING QUEUE FILE???
	JRST JOBRT

; START UP SPOOLR

STGLPT:	SYSCAL OPEN,[1000,,USRI ? 5000,,4 ? USR ? 1000,,0 ? JOBJNM]
	 POPJ P,	; CAN'T DO IT?? GIVE UP.
	SYSCAL OPEN,[1000,,DSKI ? 5000,,6 ? DSK ? JOBFN1 ? JOBFN2 ? JOBSNM]
	 POPJ P,	; SHOULDN'T LOSE IF FILES ARE WINNING
	SYSCAL LOAD,[1000,,USRI ? 1000,,DSKI]	; ONLY LOSES IF JOB IS FOREIGN
	 JRST [	SYSCAL CLOSE,[1000,,DSKI]
		 JFCL
		JRST JOBOK]
	MOVE A,[-1,,B]
	.IOT DSKI,A	; GET START-ADDRESS
	SYSCAL CLOSE,[1000,,DSKI]
	 JFCL
	HRRZS B
	.USET USRI,[.SUPC,,B]	; SET START-ADDRESS
	.USET USRI,[.STTY,,[%TBNVR,,]]	; TELL IT NOT TO HANG WAITING FOR THE TTY
	.USET USRI,[.SSNAM,,JOBSNM]
	SYSCAL DISOWN,[1000,,USRI ? 5000,,17]	; STARTS AFTER DISOWNING
	 JFCL	; SHOULD NEVER ERROR OUT.  USRI IS CLOSED.
	MOVNI A,MAXSLP
	MOVEM A,SLPCNT
	POPJ P,

INFLOD:	SYSCAL OPEN,[1000,,USRI ? 5000,,4 ? USR ? 1000,,0 ? GJNAME]
	 JRST BYE1	; CAN'T DO IT?? GIVE UP.
	SYSCAL OPEN,[1000,,DSKI ? 5000,,6 ? DSK ? 'USGLPT ? 'BIN,,0 ? JOBSNM]
	 JRST BYE1	; SHOULDN'T LOSE IF FILES ARE WINNING
	SYSCAL LOAD,[1000,,USRI ? 1000,,DSKI]	; ONLY LOSES IF JOB IS FOREIGN
	 JRST BYE1
	MOVEI B,INIT
	.USET USRI,[.SUPC,,B]	; SET START-ADDRESS
	.USET USRI,[.STTY,,[%TBNVR,,]]	; TELL IT NOT TO HANG WAITING FOR THE TTY
	.USET USRI,[.SSNAM,,JOBSNM]
	SYSCAL DISOWN,[1000,,USRI ? 5000,,17]	; STARTS AFTER DISOWNING
	 JFCL
	JRST BYE1


PDL:	BLOCK 100
PDLEND=.-PDL

	END GO
