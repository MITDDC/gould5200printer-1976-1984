	TITLE UNSPOOLER FOR A REMOTE GRAPHICS LINE PRINTER

;   THE PURPOSE OF THIS PROGRAM IS TO UNSPOOL PREPROCESSED FILES TO
; A REMOTE GRAPHICS PRINTER (CURRENTLY THE BDLG. 38 GOULD 5200).
; IT LOOKS FOR FILES DSK:GLPT;[GLPT] >, AND, IF FOUND, PUTS THEM
; OUT TO A REMOTE DEVICE BY INSERTING THE FILE INTO ITS PAGE MAP
; AND DOING AN 8-BIT SIOT TO THE DEVICE (CURRENTLY TTY T25, EVENTUALLY
; TO A DESTINATION ON THE CAIOS NET).  UPON COMPLETION, THE FILE IS
; DELETED.  IF THE LOW ORDER BIT OF THE FIRST WORD IS 1, THEN THE
; FILE IS IN 8-BIT FORMAT, OTHERWISE, ASCII.  IF THERE ARE NO FILES,
; THE PROGRAM SLEEPS FOR A WHILE.
;   THIS PROGRAM SHOULD NORMALLY BE ONLY ONE BLOCK LONG, FLUSHING
; ITS MAPPED IN CORE PAGES WHEN THEY ARE NO LONGER IN USE.

; FILE FORMAT: 
; <PAGE IDENTIFICATION NUMBER> ,, <NEXT PAGE WORD INDEX>
; <DATA> ...
;   IMPROVEMENTS NEEDED FOR CAIOS NET OPERATION:  ESTABLISH CONNECTION
; (CONTACT) TO THE REMOTE PDP-11 PRINTER/PLOTTER DRIVER SOFTWARE,
; HANDLE PRINTER NOT READY, ETC. BY HAND SHAKING WITH THE PDP-11

SUBTTL SYMBOLS AND MACROS

VERSIO==.FNAM2
; AC'S
A=1 ? B=2 ? C=3 ? D=4 ? E=5 ? F=6 ? G=10 ? H=11 ? P=17

; I/O CHANNELS
DSKI=12 ? LPTIN=13 ? LPTOUT=14 ? USRI=15

DEFINE SYSCAL NAME,ARGS
	.CALL [SETZ ? SIXBIT/NAME/ ? ARGS ((SETZ))]
TERMIN

MAPPAG==2	; MAP IN PAGES STARTING AT PAGE 2
BUFORG=MAPPAG*2000 ; ORIGIN OF MAPPED IN PAGES
SLPTIM==3.	; SLEEP TIME IN SECONDS
CONSLP==3.	; SLEEP FOR TEN SECONDS WHILE TRYING TO CONNECT
ACKCHT=301	; ACKNOWLEDGEMENT CODE TO SEE IF THE PDP-11 IS THERE
%BEGIN=205
%EOF=206
ACKCHR==177	; RETURN ACK CHAR
%CBLOK=2000	; SHOULDN'T NEED THIS IN THE NEXT I.T.S.
BUSRC=100000	; USER STOP BIT IN .USTP

SUBTTL DEVICE AND FILE CONSTANTS
	LOC 100

FDEV:	'DSK,,0
FN1:	SIXBIT /[GLPT]/
FN2:	SIXBIT /</
FSNAME:	SIXBIT/GLPT/

LPTDEV:	'T26,,
DSK:	'DSK,,0
USR:	'USR,,0

GSPFIL:	SIXBIT /[GLPT]/
GTFILE:	SIXBIT />/
LTFILE:	SIXBIT /</

GUNAME:	SIXBIT /GLPTR/
GSNAME:	SIXBIT/GLPT/
GJNAME:	'UNSPLR

SUBTTL MAIN FILE PROCESSING LOOP

GO:	.SUSET [.ROPTION,,B]		; GET OPTION BITS
	SYSCAL OPEN,[1000,,USRI ? 5000,,14 ? USR ? GUNAME ? GJNAME]
	 JRST LAUNCH			; NO SUCH JOB, OK LAUNCH UNSPOOLER
	.USET USRI,[.RUSTP,,A]		; SEE IF HE IS STOPPED.
	TLNE A,BUSRC			; IF BIT IS ZERO, JOB IS RUNNING
	 JRST DEAD
	TLNN B,OPTDDT
	 JRST BYE1
	.VALUE [ASCIZ /:UNSPOOLER ALREADY LAUNCHED AND RUNNING.î:KILLî/]
DEAD:	.USET USRI,[.RUIND,,A]		; KILL THE DEAD JOB
	.GUN A,
	SYSCAL CLOSE,[1000,,USRI]
	 JFCL
LAUNCH:	TLNE B,OPTDDT
	.VALUE [ASCIZ /17/]		; DISOWN MYSELF
INIT:	.SUSET [.SUNAM,,GUNAME]		; CHANGE MY UNAME TO GLPTR
	.SUSET [.SJNAM,,GJNAME]		; MAKE ME LOOK LIKE AN UNSPOOLER
	.SUSET [.SSNAM,,GSNAME]		; FOR THE BENEFIT OF PEEK
INIT2:	MOVE P,[-PDLEND,,PDL-1]		; INITIALIZE THE PDL
; MAIN LOOP FOR MAPPING, OUTPUTING, AND DELETING FILES
	 PUSHJ P,CONECT		; CONNECT TO TTY LINE OR CAIOS DESTINATION

WORKLP:	SYSCAL OPEN,[1000,,DSKI ? 5000,,4 ? DSK ? GSPFIL ? LTFILE ? GSNAME]
	 JRST [	PUSHJ P,SLEEPR		; NO WORK TO DO, GO SLEEP
		JRST WORKLP]
	SYSCAL RFNAME,[ [-1] ? 1000,,DSKI ? 2000,,FDEV ? 2000,,FN1 ? 2000,,FN2 ? 2000,,FSNAME]
	 JFCL		; SO WHAT IF IT DOESN'T WIN?
	SYSCAL FILLEN,[1000,,DSKI ? 2000,,A]	; GET NUMBER OF WORDS IN THE FILE
	 JFCL
	JUMPE A,SPDONE				; IGNORE NULL FILES
	MOVE  H,A
	ADDI A,1777
	LSH A,-10.				; NUMBER OF PAGES
	CAILE A,376
	 MOVEI A,376
	MOVE F,A	; SAVE FOR LATER
	MOVNS A		; COMPUTE AOBJN POINTER OF -<#PAGES>,,MAPPAG
	HRLS A
	HRRI A,MAPPAG
	MOVEM A,MEMPTR'	; SAVE AOBJN PTR FOR DELETING PAGES LATER
GETCOR:	SYSCAL CORBLK,[	1000,,<%CBNDR+%CBLOK>	; REQUEST READ ACCESS AND PAGE LOCK
			[-1] ? MEMPTR ? 1000,,DSKI]	; FOR HIGH THRU PUTT
	 JRST NOCORE
	MOVE D,[441003,,]			; 8 BIT CRUFT, INDEX THRU C
	MOVEI E,4				; WORD COUNT MULTIPLIER
	MOVE A,BUFORG				; CHECK FIRST WORD
	TRNE A,1
	 JRST OUTPUT				; BIT NOT 0, MUST BE 8-BIT
	MOVE D,[440703,,]			; ASCII FILE
	MOVEI E,5
OUTPUT:	MOVEI A,%BEGIN
	PUSHJ P,8TYO
	MOVEI A,BUFORG
	LSH F,10.				; COMPUTE END OF CORE
	ADDI F,BUFORG-1
	ADDI H,BUFORG
	PUSHJ P,PAGE
	CAIN A,(H)
	 JRST SPDONE
OUTLP:	MOVEI C,(A)
	MOVEI B,(C)
	PUSHJ P,PAGE
	SUBM A,B
	IMUL B,E				; GET BYTE COUNT
	SYSCAL SIOT,[1000,,LPTOUT ? D ? B]
	 JFCL
	HLRZ B,(C)				; PAGE IDENTIFIER
	LSH B,-10.
	ANDI B,77
	MOVEM C,PAGTAB(B)			; SAVE THE INDEX
	.IOT LPTIN,0	; GOBBLE ANYTHING THERE
	JUMPG 0,.-1
	CAIE A,(H)
	 JRST OUTLP

SPDONE:	MOVEI A,%EOF
	PUSHJ P,8TYO
	SYSCAL CORBLK,[1000,,0 ? [-1] ? MEMPTR ]	; DELETE PAGES
	 JFCL
	SYSCAL DELETE,[FDEV ? FN1 ? FN2 ? FSNAME] ; DELETE THE FILE
	 JFCL	; JUST IGNORE IT IF WE CAN'T DELETE IT
	SYSCAL CLOSE,[1000,,DSKI]	; FILE ACTUALLY GOES AWAY NOW
	 JFCL	; NEVER FAILS
	JRST WORKLP

PAGELP:	MOVE G,(A)		; DOESNT WORK FOR ASCII FILES YET
	CAIE A,(H)		; END OF FILE
	TRNE G,4
	 POPJ P,
PAGE:	AOJA A,PAGELP

SUBTTL ESTABLISH CONNECTION TO REMOTE LPT DEVICE

; OPEN A CHANNEL TO THE PDP-11 AND VERIFY THAT IT IS RUNNING.  IF NOT
; SLEEP A WHILE AND TRY AGAIN.  IN THE CAIOS IMPLEMENTATION, SEE IF
; A CONNECTION EXISTS.  IF NOT, CONTACT THE PRINTER DRIVER IN THE PDP-11.

CONECT:	SYSCAL OPEN,[1000,,LPTOUT ? 5000,,45 ? LPTDEV] ; UNIT SUPERIMAGE OUTPUT
	 JRST LPTNA	; GLPT NOT AVAILABLE EVIDENTLY
	SYSCAL FLUSH,[1000,,LPTOUT]
	 JFCL
	SYSCAL OPEN,[1000,,LPTIN ? 5000,,7004 ? LPTDEV]	; ACKNOWLEDGEMENT CHANNEL
			; IMAGE UNIT INPUT, DON'T WAIT IF NOTHING THERE
	 JRST LPTNA	; IF OPEN FOR OUTPUT, WHY NOT INPUT?
ACK11:	MOVEI A,ACKCHT	; SEND OUT A REQUEST FOR ACKNOWLEDGEMENT
	PUSHJ P,8TYO
	.IOT LPTIN,A	; GET A POSITIVE RESPONSE
	CAIN A,ACKCHR
	 JRST CONEXT	; ALL CONNECTED TO THE PDP-11
	MOVEI C,CONSLP*30.
	.SLEEP C,
	JRST ACK11

CONEXT:	.IOT LPTIN,A	; GOBBLE BYTE 1
	.IOT LPTIN,A	; GOBBLE BYTE 2
	.IOT LPTIN,A	; GOBBLE BYTE 3
	.IOT LPTIN,A	; GOBBLE BYTE 4
	POPJ P,
	
SUBTTL HANDLER OF VARIOUS ANOMALOUS CONDITIONS

LPTNA:	PUSHJ P,SLEEPR
	JRST CONECT

NOCORE:	PUSHJ P,SLEEPR	; TRY LATER
	JRST GETCOR

SLEEPR:	MOVEI C,SLPTIM*30.
	.SLEEP C,
	POPJ P,

8TYO:	SYSCAL IOT,[1000,,LPTOUT ? [%TDQOT]]
	 JRST BYE
	SYSCAL IOT,[1000,,LPTOUT ? A]
	 JRST BYE
	SYSCAL FLUSH,[1000,,LPTOUT]
	 JFCL
	POPJ P,

BYE:	SYSCAL LOGOUT,[1000,,0]
BYE1:	.BREAK 16,40000

PDL:	BLOCK 100
PDLEND=.-PDL

PAGTAB:	BLOCK 77

	END GO
